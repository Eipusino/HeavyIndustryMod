import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
	id "java"
}

version "1.0.9"

sourceSets.main.java.srcDir("src")

repositories {
	mavenCentral()
	maven { url "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
	maven { url "https://www.jitpack.io" }
}

java {
	targetCompatibility = JavaVersion.VERSION_17
	sourceCompatibility = JavaVersion.VERSION_17
}

ext {
	//windows sucks
	isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
	sdkRoot = System.getenv("ANDROID_HOME") ?: System.getenv("ANDROID_SDK_ROOT")
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"

	options.compilerArgs.addAll([
			"--add-exports", "java.base/jdk.internal.misc=ALL-UNNAMED"
	])
}

tasks.withType(KotlinCompile).configureEach {
	compilerOptions.jvmTarget.set(JvmTarget.JVM_17)
}

dependencies {
	compileOnly "com.github.Anuken.Arc:arc-core:$mindustryVersion"
	compileOnly "com.github.Anuken.Mindustry:core:$mindustryVersion"
	compileOnly project(":annotations")

	implementation "org.jetbrains:annotations:26.0.2"
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
	implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

	implementation "org.ow2.asm:asm:$asmVersion"
	implementation "org.ow2.asm:asm-tree:$asmVersion"
	implementation "org.ow2.asm:asm-commons:$asmVersion"

	implementation "org.commonmark:commonmark:$commonmarkVersion"
	implementation "org.commonmark:commonmark-ext-gfm-tables:$commonmarkVersion"
	implementation "org.commonmark:commonmark-ext-gfm-strikethrough:$commonmarkVersion"
	implementation "org.commonmark:commonmark-ext-ins:$commonmarkVersion"

	annotationProcessor project(":annotations")
}

task jarAndroid {
	dependsOn "jar"

	doLast {
		if (!sdkRoot || !new File(sdkRoot).exists()) throw new GradleException("No valid Android SDK found. Ensure that ANDROID_HOME is set to your Android SDK directory.")

		def platformRoot = new File("$sdkRoot/platforms/").listFiles().sort().reverse().find { f -> new File(f, "android.jar").exists() }

		if (!platformRoot) throw new GradleException("No android.jar found. Ensure that you have an Android platform installed.")

		//collect dependencies needed for desugaring
		def dependencies = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File(platformRoot, "android.jar")]).collect { "--classpath $it.path" }.join(" ")

		def d8 = isWindows ? "d8.bat" : "d8"

		//dex and desugar files - this requires d8 in your PATH
		"$d8 $dependencies --min-api 28 --output ${outputJarName}Android.jar ${outputJarName}Desktop.jar"
				.execute(null, new File("$buildDir/libs")).waitForProcessOutput(System.out, System.err)
	}
}

jar {
	archiveFileName = "${outputJarName}Desktop.jar"
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}

	from(rootDir) {
		include "mod.json"
	}

	from("assets/") {
		include "**"
	}
}

task deploy(type: Jar) {
	dependsOn jarAndroid
	dependsOn jar
	archiveFileName = "${outputJarName}.jar"

	from { [zipTree("$buildDir/libs/${outputJarName}Desktop.jar"), zipTree("$buildDir/libs/${outputJarName}Android.jar")] }

	doLast {
		delete {
			delete "$buildDir/libs/${outputJarName}Desktop.jar"
			delete "$buildDir/libs/${outputJarName}Android.jar"
		}
	}
}